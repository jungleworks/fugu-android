apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco'
apply plugin: 'com.github.dcendents.android-maven'

group 'com.percolate.mentions'
version '1.0-SNAPSHOT'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 10
        versionName "0.1.0"
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES'
    }
    buildTypes {
        debug {
            testCoverageEnabled false
        }
    }
    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenCentral()
}

jacoco {
    version = '0.7.6.201602180812'
}

task coverage(type:JacocoReport, dependsOn: "testDebugUnitTest") {
    group = "Reporting"
    description = 'Create code coverage report for library.'

    getClassDirectories().setFrom(fileTree(
            dir: "$buildDir/intermediates/classes/debug",
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/BuildConfig.*',
                       'com/percolate/mentions/CharSequenceUtils.class',
                       'com/percolate/mentions/StringUtils.class']
    ))

    getSourceDirectories().setFrom(files("src/main/java"))
    getExecutionData().setFrom(files("build/jacoco/testDebugUnitTest.exec"))

    reports {
        //noinspection GroovyAssignabilityCheck
        xml.enabled = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.2.0'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.robolectric:robolectric:4.2.1'
    testImplementation 'org.mockito:mockito-core:2.23.0'
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
